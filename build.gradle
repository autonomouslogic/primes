plugins {
	id "java"
	id "idea"
	id "com.diffplug.spotless" version "7.0.2"
	id "io.freefair.lombok" version "8.12.2.1"
}

group = "com.autonomouslogic.primes"
version = "dev"

ext {
	envProperties = new Properties()
	if (file("env/local.env").exists()) {
		file("env/local.env").withInputStream { envProperties.load(it) }
	}
}

java {
	toolchain {
		sourceCompatibility = JavaVersion.VERSION_21
		targetCompatibility = JavaVersion.VERSION_21
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation "com.autonomouslogic.commons:commons-java:1.9.5"
	implementation "org.apache.logging.log4j:log4j-api:2.24.3"
	implementation "org.apache.logging.log4j:log4j-core:2.24.3"
	implementation "org.apache.logging.log4j:log4j-slf4j2-impl:2.24.3"
	implementation "com.google.guava:guava:33.4.8-jre"
	implementation "commons-io:commons-io:2.18.0"
	implementation "com.fasterxml.jackson.core:jackson-annotations:2.18.3"
	implementation "com.fasterxml.jackson.core:jackson-core:2.18.3"
	implementation "com.fasterxml.jackson.core:jackson-databind:2.18.3"
	implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.18.3"

	testImplementation platform("org.junit:junit-bom:5.10.0")
	testImplementation "org.junit.jupiter:junit-jupiter"
}

test {
	useJUnitPlatform()
	testLogging {
		events = [ "FAILED", "PASSED", "SKIPPED" ]
		showExceptions = true
		exceptionFormat = "FULL"
		showExceptions = true
		showCauses = true
		showStackTraces = true
	}
}

spotless {
	java {
		target "src/**/*.java"
		palantirJavaFormat()
		leadingSpacesToTabs()
	}

	groovyGradle {
		leadingSpacesToTabs()
	}
}

wrapper {
	distributionType = "ALL"
}

tasks.register('primeSearch', JavaExec) {
	group "application"
	environment envProperties
	classpath = sourceSets.main.runtimeClasspath
	mainClass = "com.autonomouslogic.primes.PrimeSearch"
	jvmArgs = [
			"-Xmx20G",
	]
}
